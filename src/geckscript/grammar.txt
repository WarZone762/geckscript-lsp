GRAMMAR
// TODO: document full grammar

literal:
  NUMBER | STRING

variable:
  ID

keyword:
  KEYWORD

variable_declaration:
  TYPE variable

variable_or_variable_declaration:
  variable | variable_declaration

function:
  FUNCTION (shift_expression COMMA?)*

lambda:
  LBRACKET variable_or_variable_declaration* RBRACKET EQUALS_GREATER expression |
  BEGIN BLOCK_TYPE_FUNCTION LBRACKET variable_or_variable_declaration* RBRACKET NEWLINE
    block
  END

primary_expression:
  literal | variable | function | LPAREN (expression | lambda) RPAREN

member_expression:
  primary_expression |
  member_expression LSQ_BRACKET expression RSQ_BRACKET |
  member_expression RARROW (ID | STRING | NUMBER | FUNCTION) |
  member_expression DOT (ID | FUNCTION)

logical_not_expression:
  member_expression |
  EXCLAMATION logical_not_expression

unary_expression:
  logical_not_expression |
  (MINUS | DOLLAR | HASH | ASTERISK | AMPERSAND) unary_expression

exponential_expression:
  unary_expression (CIRCUMFLEX unary_expression)*

multiplicative_expression:
  exponential_expression ((ASTERISK | SLASH | PERCENT) exponential_expression)*

additive_expression:
  multiplicative_expression ((PLUS | MINUS) multiplicative_expression)*

shift_expression:
  additive_expression ((DOUBLE_LESS | DOUBLE_GREATER) additive_expression)*

and_expression:
  shift_expression (AMPERSAND shift_expression)*

or_expression:
  and_expression (VBAR and_expression)*

relational_expression:
  or_expression ((GREATER | GREATER_EQUALS | LESS | LESS_EQUALS) or_expression)*

equality_expression:
  relational_expression ((DOUBLE_EQUALS | EXCLAMATION_EQUALS) relational_expression)*

slice_make_pair_expression:
  equality_expression ((COLON | DOUBLE_COLON) equality_expression)*

logical_and_expression:
  slice_make_pair_expression ((
    DOUBLE_AMPERSAND
  ) slice_make_pair_expression)*

logical_or_expression:
  logical_and_expression (DOUBLE_VBAR logical_and_expression)*

assignment_expression:
  ((member_expression (EQUALS | COLON_EQUALS)) | assignment_expression)? logical_or_expression

expression:
  assignment_expression

assignment_set:
  SET variable TO logical_or_expression

assignment_let:
  let assignment_expression

statement:
  (COMMENT |
   assignment_set |
   assignment_let |
   while_block |
   foreach_block |
   if_block |
   variable_declaration_statement |
   expression) (NEWLINE | EOF)

block:
  statement*

variable_declaration_statement:
  variable_declaration ((
    EQUALS |
    COLON_EQUALS |
    PLUS_EQUALS |
    MINUS_EQUALS |
    ASTERISK_EQUALS |
    SLASH_EQUALS |
    CIRCUMFLEX_EQUALS |
    VBAR_EQUALS |
    AMPERSAND_EQUALS |
    PERCENT_EQULAS
    ) expression)?

block_type:
  BLOCK_TYPE primary_expression*

begin_block:
  BEGIN block_type NEWLINE
    block
  END

if_block:
  IF expression NEWLINE
    block
  (ELSEIF expression NEWLINE
      block)*
  (ELSE NEWLINE
    block)?
  ENDIF

while_block:
  WHILE expression NEWLINE
    block
  LOOP

foreach_block:
  FOREACH variable_or_variable_declaration LARROW expression NEWLINE
    block
  LOOP

script_compound_statement:
  (
    assignment_set |
    assignment_let |
    variable_declaration_statement |
    begin_block
  )*

script:
  SCRIPTNAME ID NEWLINE
  script_compound_statement
